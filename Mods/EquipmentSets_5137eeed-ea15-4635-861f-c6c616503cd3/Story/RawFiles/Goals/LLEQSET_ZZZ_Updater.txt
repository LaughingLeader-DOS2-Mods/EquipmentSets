Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLEQSET_Updater_InitSettings();
LeaderUpdater_Register_Mod("EquipmentSets", "LaughingLeader", "0.9.0.0");
LeaderUpdater_Register_ActiveGoal("EquipmentSets", "LaughingLeader", "LLEQSET_LeaderLib");
LLEQSET_Updater_RegisterDatabasesOnce();
//DB_LLEQSET_Dependencies(_Mod, _Author, _GlobalFlag)
KBSECTION
//REGION SETTINGS
PROC
LLEQSET_Updater_InitSettings()
THEN
LLEQSET_System_ClearExistingDatabase("DB_LLEQSET_Dependencies", 2);
DB_LLEQSET_Dependencies("NamingWizard", "LaughingLeader", "LLEQSET_Global_NamingWizardFound");
//END_REGION

//REGION MOD_UPDATING
PROC
LLEQSET_System_UpdateDatabases()
THEN
LeaderLog_Log("DEBUG","[LLEQSET][Updater] Databases updated.");

PROC
LLEQSET_Updater_RegisterDatabasesOnce()
AND
NOT DB_LLEQSET_Updater_RegisteredDatabases(_)
THEN
LLEQSET_System_RegisterDatabases();
DB_LLEQSET_Updater_RegisteredDatabases(1);

PROC
LLEQSET_System_RegisterDatabases()
THEN
LeaderUpdater_Register_Database("EquipmentSets", "LaughingLeader", "Updater", "DB_LLEQSET_Updater_RegisteredDatabases", 1);
LeaderLog_Log("DEBUG","[LLEQSET][Updater] Databases registered.");


PROC
LLEQSET_Updater_UnregisterMod()
THEN
LeaderUpdater_UnregisterMod("EquipmentSets", "LaughingLeader");
//END_REGION

//REGION UPDATE_EVENT
IF
GameStarted(_,_)
AND
LeaderUpdater_QRY_ModUpdateNeeded("EquipmentSets", "LaughingLeader", "0.9.0.0")
THEN
LeaderUpdater_Register_Mod("EquipmentSets", "LaughingLeader", "0.9.0.0");
LLEQSET_System_UpdateDatabases();

/*
IF
StoryEvent(_, "LeaderLib_Initialized")
THEN
LLEQSET_System_RegisterDatabases();
*/
//END_REGION

//REGION MOD_DETECTION
PROC
LLEQSET_Updater_CheckDependencies()
AND
DB_LLEQSET_Dependencies(_Mod, _Author, _GlobalFlag)
AND
LeaderUpdater_QRY_ModIsActive(_Mod, _Author)
THEN
LeaderLog_Log("DEBUG","[LLEQSET][Updater] Mod ",_Mod," by ", _Author," is active.");
GlobalSetFlag(_GlobalFlag);

PROC
LLEQSET_Updater_CheckDependencies()
AND
DB_LLEQSET_Dependencies(_Mod, _Author, _GlobalFlag)
AND
NOT LeaderUpdater_QRY_ModIsActive(_Mod, _Author)
THEN
LeaderLog_Log("DEBUG","[LLEQSET][Updater] Mod ",_Mod," by ", _Author," not active.");
GlobalClearFlag(_GlobalFlag);
//END_REGION

//DEPENDENCY_CHECK_EVENT
IF
GameStarted(_,_)
THEN
TimerLaunch("LLEQSET_Timers_CheckDependencies", 500);

IF
TimerFinished("LLEQSET_Timers_CheckDependencies")
THEN
LLEQSET_Updater_CheckDependencies();
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_EquipmentSets"
