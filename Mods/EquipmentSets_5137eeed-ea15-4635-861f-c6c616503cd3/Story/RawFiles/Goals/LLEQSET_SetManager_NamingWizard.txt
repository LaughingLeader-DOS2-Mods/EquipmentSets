Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLEQSET_NamingInterface_InitSettings();
//DB_LLEQSET_NamingWizard_UsingWizard(_Player, _TimerActive);
//DB_LLEQSET_NamingWizard_Words(_Index, _Word);
//DB_LLEQSET_NamingWizard_RegisteredWordMenu(_Val)
KBSECTION
//REGION SETTINGS
PROC
LLEQSET_NamingInterface_InitSettings()
THEN
LLEQSET_NamingInterface_AddWord("Set");
LLEQSET_NamingInterface_AddWord("Equipment");
LLEQSET_NamingInterface_AddWord("Main");
LLEQSET_NamingInterface_AddWord("Alt");
LLEQSET_NamingInterface_AddWord("Civil");
LLEQSET_NamingInterface_AddWord("Barter");
LLEQSET_NamingInterface_AddWord("Luck");
LLEQSET_NamingInterface_AddWord("Sneaking");
LLEQSET_NamingInterface_AddWord("Thievery");
//END_REGION

//REGION UPDATER
PROC
LeaderUpdater_ModUpdated("EquipmentSets", "LaughingLeader", _Version)
AND
_Version == "-1"
THEN
LLEQSET_NamingInterface_InitSettings();
LeaderLog_Log("DEBUG","[LLEQSET:NamingInterface] Databases updated.");
//END_REGION

//REGION WORDS
PROC
LLEQSET_NamingInterface_AddWord((STRING)_Word)
AND
NOT DB_LLEQSET_NamingWizard_Words(_, _Word)
AND
SysCount("DB_LLEQSET_NamingWizard_Words", 2, _Index)
THEN
DB_LLEQSET_NamingWizard_Words(_Index, _Word);

PROC
LLNWIZ_WordMenus_LoadWords("LLEQSET_EquipmentSetWords")
AND
DB_LLEQSET_NamingWizard_Words(_Index, _Word)
THEN
DB_LLNWIZ_Words_Displayed(_Index, _Word);
//END_REGION

//REGION NAMING_WIZARD_DETECTION
IF
StoryEvent(_, "LeaderLib_Initialized")
AND
NOT DB_LLEQSET_NamingWizard_RegisteredWordMenu(_)
AND
LeaderUpdater_QRY_ModIsRegistered("NamingWizard", "LaughingLeader")
THEN
DB_LLNWIZ_ModApi_RegisterMenu("LLEQSET_EquipmentSetWords", "Equipment Set Words", "DB_LLEQSET_NamingWizard_Words", 2);
DB_LLNWIZ_ModApi_RegisterWord("Mod", "Equipment Sets");
DB_LLEQSET_NamingWizard_RegisteredWordMenu(0);

IF
StoryEvent(_, "LeaderLib_Events_OnDefaultEventFlowComplete")
AND
DB_LLEQSET_NamingWizard_RegisteredWordMenu(0)
AND
NOT DB_LLNWIZ_Words_CurrentlySorting("Mod")
THEN
NOT DB_LLEQSET_NamingWizard_RegisteredWordMenu(0);
DB_LLEQSET_NamingWizard_RegisteredWordMenu(1);
DB_LLNWIZ_ModApi_SortMenu("Mod");
//DB_LLNWIZ_ModApi_SortMenu("Names");
//END_REGION

//REGION CLEAR
PROC
LLEQSET_NamingInterface_ClearNamingWizardInstanceSettings((CHARACTERGUID)_Player)
THEN
DB_LLNWIZ_ModApi_ClearSessionVariables(_Player);

PROC
LLEQSET_NamingInterface_ClearNamingWizardInstanceSettings((CHARACTERGUID)_Player)
AND
DB_LLEQSET_NamingWizard_UsingWizard(_Player, _TimerActive)
THEN
NOT DB_LLEQSET_NamingWizard_UsingWizard(_Player, _TimerActive);

PROC
LLEQSET_NamingInterface_ClearNamingWizardInstanceSettings((CHARACTERGUID)_Player)
THEN
ObjectClearFlag(_Player, "LLEQSET_NamingWizard_Running");
ObjectClearFlag(_Player, "LeaderLib_BlockModMenuReopen");
SetVarString(_Player, "LLNWIZ_TargetName", "");
SetVarString(_Player, "LLNWIZ_CustomNameOverride", "");
//END_REGION

//REGION NAMING_WIZARD_MENU
IF
DialogEnded("LLEQSET_EquipmentSetMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player, "LLEQSET_Menu_OpenNamingMenu", 1)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Menu_OpenNamingMenu")
THEN
LLEQSET_NamingInterface_InitNamingWizardSettings(_Player);
ObjectSetFlag(_Player, "LLEQSET_NamingWizard_Running");
SetStoryEvent(_Player, "LLNWIZ_Events_OpenNamingWizard");
DB_LLEQSET_NamingWizard_UsingWizard(_Player, 1);

PROC
LLEQSET_NamingInterface_InitNamingWizardSettings((CHARACTERGUID)_Player)
AND
DB_LLEQSET_Menu_SelectedSet(_Player, _SetID)
AND
DB_LLEQSET_SetManager_DefaultSetNames(_SetID, _DefaultName)
THEN
SetVarString(_Player, "LLNWIZ_TargetName", _DefaultName);

PROC
LLEQSET_NamingInterface_InitNamingWizardSettings((CHARACTERGUID)_Player)
AND
DB_LLEQSET_Menu_SelectedSet(_Player, _SetID)
AND
DB_LLEQSET_SetManager_SetNames(_Player, _SetID, _Name)
THEN
SetVarString(_Player, "LLNWIZ_CustomNameOverride", _Name);
ObjectClearFlag(_Player, "LLNWIZ_Naming_HideCustomName");

PROC
LLEQSET_NamingInterface_InitNamingWizardSettings((CHARACTERGUID)_Player)
AND
DB_LLEQSET_Menu_SelectedSet(_Player, _SetID)
AND
NOT DB_LLEQSET_SetManager_SetNames(_Player, _SetID, _)
THEN
ObjectSetFlag(_Player, "LLNWIZ_Naming_HideCustomName");

IF
StoryEvent((CHARACTERGUID)_Player, "LLNWIZ_Events_NamingWizardOpened")
AND
ObjectGetFlag(_Player, "LLEQSET_NamingWizard_Running", 1)
THEN
TimerLaunch("LLEQSET_NamingWizard_Timeout", 5000);

IF
DialogStarted("LLNWIZ_NamingWizard", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player, "LLEQSET_NamingWizard_Running", 1)
AND
DB_LLEQSET_NamingWizard_UsingWizard(_Player, _TimerActive)
THEN
NOT DB_LLEQSET_NamingWizard_UsingWizard(_Player, _TimerActive);
DB_LLEQSET_NamingWizard_UsingWizard(_Player, 0);
TimerCancel("LLEQSET_NamingWizard_Timeout");
ObjectSetFlag(_Player, "LeaderLib_BlockModMenuReopen");

IF
TimerFinished("LLEQSET_NamingWizard_Timeout")
AND
DB_LLEQSET_NamingWizard_UsingWizard(_Player, 1)
THEN
ObjectClearFlag(_Player, "LLEQSET_Naming_TextSaved");
LeaderLog_Log("DEBUG","[LLEQSET:NamingWizard:Timeout] LLNWIZ_NamingWizard dialog failed to start.");
LLEQSET_NamingInterface_ClearNamingWizardInstanceSettings(_Player);
Proc_StartDialog(0, "LLEQSET_EquipmentSetMenu", _Player, _Player);

IF
DialogEnded("LLNWIZ_NamingWizard", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_LLEQSET_NamingWizard_UsingWizard(_Player, _TimerActive)
AND
ObjectGetFlag(_Player, "LLNWIZ_Naming_TextSaved", 0)
THEN
LLEQSET_NamingInterface_ClearNamingWizardInstanceSettings(_Player);
Proc_StartDialog(0, "LLEQSET_EquipmentSetMenu", _Player, _Player);

IF
StoryEvent((CHARACTERGUID)_Player, "LLNWIZ_Events_NamingComplete")
AND
ObjectGetFlag(_Player, "LLEQSET_NamingWizard_Running", 1)
AND
GetVarString(_Player, "LLNWIZ_FinalText", _Text)
AND
DB_LLEQSET_NamingWizard_UsingWizard(_Player, _TimerActive)
THEN
SetVarString(_Player, "LLNWIZ_FinalText", "");
LLEQSET_NamingInterface_ClearNamingWizardInstanceSettings(_Player);
DB_LLEQSET_Naming_FinishedText(_Text, _Player);
ObjectSetFlag(_Player, "LLEQSET_Naming_TextSaved");
Proc_StartDialog(0, "LLEQSET_EquipmentSetMenu", _Player, _Player);

IF
DialogStarted("LLEQSET_EquipmentSetMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player, "LLEQSET_Naming_TextSaved", 1)
AND
DB_LLEQSET_Naming_FinishedText(_Text, _Player)
THEN
DialogSetVariableStringForInstance(_Instance, "LLEQSET_Naming_FinalText_7a7aaab8-c19d-4af5-9d1b-9a1b5d7a47c0", _Text);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_EquipmentSets"
