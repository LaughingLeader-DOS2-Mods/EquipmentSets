Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLEQSET_Menu_InitializeSettings();
//DB_LLEQSET_SetManager_DefaultSetNames(_SetID, _DefaultName)
//DB_LLEQSET_SetManager_Temp_ActiveDialog(_Player, _Instance)
KBSECTION
//REGION SETTINGS_AND_UPDATER
PROC
LLEQSET_Menu_InitializeSettings()
THEN
LLEQSET_System_ClearExistingDatabase("DB_LLEQSET_SetManager_DefaultSetNames", 2);
DB_LLEQSET_SetManager_DefaultSetNames("Set1", "Set 1");
DB_LLEQSET_SetManager_DefaultSetNames("Set2", "Set 2");
DB_LLEQSET_SetManager_DefaultSetNames("Set3", "Set 3");
DB_LLEQSET_SetManager_DefaultSetNames("Set4", "Set 4");
DB_LLEQSET_SetManager_DefaultSetNames("Set5", "Set 5");

PROC
LeaderUpdater_ModUpdated("EquipmentSets", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
_PastVersion == "-1"
THEN
LLEQSET_Menu_InitializeSettings();
//END_REGION

//REGION CLEAR
PROC
LLEQSET_Menu_ClearMenuFlags((CHARACTERGUID)_Player)
THEN
ObjectClearFlag(_Player, "LLEQSET_Menu_CanSaveSet");
ObjectClearFlag(_Player, "LLEQSET_Menu_CurrentSetEquipped");
ObjectClearFlag(_Player, "LLEQSET_Menu_CurrentSetSaved");

PROC
LLEQSET_Menu_ClearSetName((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_SetManager_SetNames(_Player, _SetID, _Name)
THEN
NOT DB_LLEQSET_SetManager_SetNames(_Player, _SetID, _Name);
//END_REGION

//REGION DIALOG_VARS
PROC
LLEQSET_Menu_LoadSetNames((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLEQSET_SetManager_SetNames(_Player, _SetID, _Name)
AND
DB_LLEQSET_SetManager_Variables(_SetID, _DialogNameVar, _SelectFlag, _EquipSetFlag)
THEN
DialogSetVariableStringForInstance(_Instance, _DialogNameVar, _Name);

PROC
LLEQSET_Menu_SetCurrentSetName((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLEQSET_Menu_SelectedSet(_Player, _SetID)
THEN
LLEQSET_Menu_SetCurrentSetName(_Player, _Instance, _SetID);

PROC
LLEQSET_Menu_SetCurrentSetName((CHARACTERGUID)_Player, (INTEGER)_Instance, (STRING)_SetID)
AND
DB_LLEQSET_SetManager_SetNames(_Player, _SetID, _Name)
AND
DB_LLEQSET_SetManager_DefaultSetNames(_SetID, _DefaultName)
AND
StringConcatenate(_Name, " (", _Str1)
AND
StringConcatenate(_Str1, _DefaultName, _Str2)
AND
StringConcatenate(_Str2, ")", _DisplayedName)
THEN
DialogSetVariableStringForInstance(_Instance, "LLEQSET_Menu_CurrentSetName_89e7900d-a560-496f-83d8-9d6593bc0f3e", _DisplayedName);

PROC
LLEQSET_Menu_SetCurrentSetName((CHARACTERGUID)_Player, (INTEGER)_Instance, (STRING)_SetID)
AND
NOT DB_LLEQSET_SetManager_SetNames(_Player, _SetID, _)
THEN
DialogSetVariableStringForInstance(_Instance, "LLEQSET_Menu_CurrentSetName_89e7900d-a560-496f-83d8-9d6593bc0f3e", _SetID);

PROC
LLEQSET_Menu_SetActiveSetNames((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
NOT DB_LLEQSET_SetManager_CurrentSet(_Player, _)
THEN
DialogSetVariableStringForInstance(_Instance, "LLEQSET_Menu_CurrentSet_b7d19a06-c548-4fad-91b7-19f0fbeea6d2", "");

PROC
LLEQSET_Menu_SetActiveSetNames((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
AND
DB_LLEQSET_SetManager_SetNames(_Player, _SetID, _Name)
AND
StringConcatenate("Current: ", _Name, _SetName)
THEN
DialogSetVariableStringForInstance(_Instance, "LLEQSET_Menu_CurrentSet_b7d19a06-c548-4fad-91b7-19f0fbeea6d2", _SetName);

PROC
LLEQSET_Menu_SetActiveSetNames((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
AND
NOT DB_LLEQSET_SetManager_SetNames(_Player, _SetID,_)
AND
StringConcatenate("Current: ", _SetID, _SetName)
THEN
DialogSetVariableStringForInstance(_Instance, "LLEQSET_Menu_CurrentSet_b7d19a06-c548-4fad-91b7-19f0fbeea6d2", _SetName);

PROC
LLEQSET_Menu_SetActiveSetNames((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
NOT DB_LLEQSET_SetManager_LastSet(_Player, _)
THEN
DialogSetVariableStringForInstance(_Instance, "LLEQSET_Menu_LastSet_4b15c955-324e-4236-8107-94259433de22", "");

PROC
LLEQSET_Menu_SetActiveSetNames((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLEQSET_SetManager_LastSet(_Player, _SetID)
AND
DB_LLEQSET_SetManager_SetNames(_Player, _SetID, _Name)
AND
StringConcatenate("Last: ", _Name, _SetName)
THEN
DialogSetVariableStringForInstance(_Instance, "LLEQSET_Menu_LastSet_4b15c955-324e-4236-8107-94259433de22", _SetName);

PROC
LLEQSET_Menu_SetActiveSetNames((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLEQSET_SetManager_LastSet(_Player, _SetID)
AND
NOT DB_LLEQSET_SetManager_SetNames(_Player, _SetID,_)
AND
StringConcatenate("Last: ", _SetID, _SetName)
THEN
DialogSetVariableStringForInstance(_Instance, "LLEQSET_Menu_LastSet_4b15c955-324e-4236-8107-94259433de22", _SetName);

PROC
LLEQSET_Menu_SetSwitcherVars((CHARACTERGUID)_Player, (STRING)_SetID, (INTEGER)_Instance)
AND
DB_LLEQSET_SetManager_SetConfig(_SetID, _Skill, _ItemTemplate, _BackpackTemplate)
THEN
DialogSetVariableStringForInstance(_Instance, "LLEQSET_Menu_SetSwitcherTemplate_e65c5fe3-a8f4-451e-b647-dfee50984e44", _ItemTemplate);

PROC
LLEQSET_Menu_SetSwitcherVars((CHARACTERGUID)_Player, (STRING)_SetID, (INTEGER)_Instance)
AND
DB_LLEQSET_SetManager_SetConfig(_SetID, _Skill, _ItemTemplate, _BackpackTemplate)
AND
CharacterHasSkill(_Player, _Skill, 1)
THEN
ObjectSetFlag(_Player,"LLEQSET_Menu_HasSelectedSetSkill");
//END_REGION

//REGION SET_SELECTION
PROC
DB_LLEQSET_Menu_ClearSelectedSet((CHARACTERGUID)_Player)
AND
DB_LLEQSET_Menu_SelectedSet(_Player, _SetID)
THEN
NOT DB_LLEQSET_Menu_SelectedSet(_Player, _SetID);

PROC
LLEQSET_Menu_SelectSet((CHARACTERGUID)_Player, (STRING)_SetID)
THEN
DB_LLEQSET_Menu_ClearSelectedSet(_Player);
DB_LLEQSET_Menu_SelectedSet(_Player, _SetID);

PROC
LLEQSET_Menu_SelectSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
THEN
ObjectSetFlag(_Player, "LLEQSET_Menu_CurrentSetEquipped");

PROC
LLEQSET_Menu_SelectSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
NOT DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
THEN
ObjectClearFlag(_Player, "LLEQSET_Menu_CurrentSetEquipped");

PROC
LLEQSET_Menu_SelectSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID,_,_)
AND
ObjectGetFlag(_Player, "LLEQSET_Menu_CurrentSetSaved", 0)
THEN
ObjectSetFlag(_Player, "LLEQSET_Menu_CurrentSetSaved");

IF
ObjectFlagCleared("LLEQSET_CurrentMenu_SetMenu", (CHARACTERGUID)_Player, _Instance)
THEN
DB_LLEQSET_Menu_ClearSelectedSet(_Player);
ObjectClearFlag(_Player, "LLEQSET_Menu_CurrentSetSaved");
ObjectClearFlag(_Player, "LLEQSET_Menu_CurrentSetEquipped");
ObjectClearFlag(_Player, "LLEQSET_Menu_HasSelectedSetSkill");

PROC
LLEQSET_Menu_InitMenuFlags((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
LLEQSET_QRY_PlayerHasEquipment(_Player)
THEN
ObjectSetFlag(_Player, "LLEQSET_Menu_CanSaveSet");

PROC
LLEQSET_Menu_InitMenuFlags((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
NOT LLEQSET_QRY_PlayerHasEquipment(_Player)
AND
ObjectGetFlag(_Player, "LLEQSET_Settings_SaveEmptySlots", 1)
THEN
ObjectSetFlag(_Player, "LLEQSET_Menu_CanSaveSet");

IF
ObjectFlagSet("LLEQSET_Settings_SaveEmptySlots", (CHARACTERGUID)_Player, _Instance)
THEN
ObjectSetFlag(_Player, "LLEQSET_Menu_CanSaveSet");

IF
ObjectFlagCleared("LLEQSET_Settings_SaveEmptySlots", (CHARACTERGUID)_Player, _Instance)
AND
NOT LLEQSET_QRY_PlayerHasEquipment(_Player)
THEN
ObjectClearFlag(_Player, "LLEQSET_Menu_CanSaveSet");

PROC
LLEQSET_Menu_InitMenuFlags((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
ObjectGetFlag(_Player, "LLEQSET_Menu_HasMenuSkill", 0)
AND
DB_LLEQSET_MenuSkill(_Skill)
AND
CharacterHasSkill(_Player, _Skill, 1)
THEN
ObjectSetFlag(_Player, "LLEQSET_Menu_HasMenuSkill");

PROC
LLEQSET_Menu_InitMenuFlags((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
ObjectGetFlag(_Player, "LLEQSET_Menu_HasMenuSkill", 1)
AND
DB_LLEQSET_MenuSkill(_Skill)
AND
CharacterHasSkill(_Player, _Skill, 0)
THEN
ObjectClearFlag(_Player, "LLEQSET_Menu_HasMenuSkill");
//END_REGION

//REGION SET_ACTIONS
IF
ObjectFlagSet(_SelectFlag, (CHARACTERGUID)_Player, _Instance)
AND
DB_LLEQSET_SetManager_Variables(_SetID, _DialogNameVar, _SelectFlag, _EquipSetFlag)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, _SelectFlag)
THEN
LLEQSET_Menu_SelectSet(_Player, _SetID);
LLEQSET_Menu_SetSwitcherVars(_Player, _SetID, _Instance);
LLEQSET_Menu_SetCurrentSetName(_Player, _Instance, _SetID);

IF
ObjectFlagSet(_EquipSetFlag, (CHARACTERGUID)_Player, _Instance)
AND
DB_LLEQSET_SetManager_Variables(_SetID, _DialogNameVar, _SelectFlag, _EquipSetFlag)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, _EquipSetFlag)
THEN
LLEQSET_SetManager_ChangeSet(_Player, _SetID);

IF
ObjectFlagSet("LLEQSET_Actions_UnequipCurrentSet", (CHARACTERGUID)_Player, _Instance)
AND
NOT DB_LLEQSET_SetManager_CurrentSet(_Player, _)
THEN
LeaderLog_Log("DEBUG","[LLEQSET:SetManager:Menu] [ERROR] No current set for player.");

IF
ObjectFlagSet("LLEQSET_Actions_UnequipCurrentSet", (CHARACTERGUID)_Player, _Instance)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Actions_UnequipCurrentSet")
THEN
LLEQSET_Menu_Internal_UnequipCurrentSet(_Player);

/*These CharacterGetEquippedItem queries don't return anything in this context, for some reason.
PROC
LLEQSET_Menu_Internal_UnequipCurrentSet((CHARACTERGUID)_Player)
AND
DB_LeaderLib_EquipmentSlots(_Slot)
AND
CharacterGetEquippedItem(_Player, _Slot, _EquippedItem)
THEN
LeaderLog_Log("DEBUG","[LLEQSET:SetManager:Menu] Item is in slot [",_Slot,"].");

PROC
LLEQSET_Menu_Internal_UnequipCurrentSet((CHARACTERGUID)_Player)
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
AND
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, _Item)
AND
GetUUID(_Item, _ItemCheckID)
AND
LeaderLog_QRY_Log("DEBUG","[LLEQSET:SetManager:Menu] Checking slot [",_Slot,"] for item [",_ItemCheckID,"] in set [",_SetID,"].")
AND
CharacterGetEquippedItem(_Player, _Slot, _EquippedItem)
AND
GetUUID(_EquippedItem, _ItemID)
THEN
LeaderLog_Log("DEBUG","[LLEQSET:SetManager:Menu] Item [",_ItemID,"] is in slot [",_Slot,"] for set [",_SetID,"].");
*/

PROC
LLEQSET_Menu_Internal_UnequipCurrentSet((CHARACTERGUID)_Player)
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
AND
LLEQSET_QRY_SetNotEmpty(_Player, _SetID)
THEN
LLEQSET_SetManager_PlayOverlayEffect(_Player, _SetID);

PROC
LLEQSET_Menu_Internal_UnequipCurrentSet((CHARACTERGUID)_Player)
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
THEN
LLEQSET_SetManager_UnequipSet(_Player, _SetID);
LLEQSET_SetManager_ResetCurrentSet(_Player);
ObjectClearFlag(_Player, "LLEQSET_Menu_CurrentSetEquipped");

IF
ObjectFlagSet("LLEQSET_Actions_EquipCurrentSet", (CHARACTERGUID)_Player, _Instance)
AND
DB_LLEQSET_Menu_SelectedSet(_Player, _SetID)
AND
LLEQSET_QRY_SetManager_ShouldPlayEffect(_Player, _SetID)
THEN
LLEQSET_SetManager_PlayOverlayEffect(_Player, _SetID);
LLEQSET_SetManager_PlayCastEffect(_Player, _SetID);

IF
ObjectFlagSet("LLEQSET_Actions_EquipCurrentSet", (CHARACTERGUID)_Player, _Instance)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Actions_EquipCurrentSet")
AND
DB_LLEQSET_Menu_SelectedSet(_Player, _SetID)
THEN
//LeaderLog_Log("TRACE","[LLEQSET:SetManager:Menu] Equipped current set [",_SetID,"].");
LLEQSET_SetManager_ChangeSet(_Player, _SetID);
LLEQSET_Menu_SetActiveSetNames(_Player, _Instance);
ObjectSetFlag(_Player, "LLEQSET_Menu_CurrentSetEquipped");

IF
ObjectFlagSet("LLEQSET_Actions_SaveCurrentEquipment", (CHARACTERGUID)_Player, _Instance)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Actions_SaveCurrentEquipment")
AND
DB_LLEQSET_Menu_SelectedSet(_Player, _SetID)
THEN
LLEQSET_SetManager_RegisterAllEquipment(_Player, _SetID);
LeaderLib_SetManager_Menu_SetEmptyCurrentSet(_Player, _SetID);
ObjectSetFlag(_Player, "LLEQSET_Menu_CurrentSetSaved");

PROC
LeaderLib_SetManager_Menu_SetEmptyCurrentSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
NOT DB_LLEQSET_SetManager_CurrentSet(_Player, _)
THEN
LLEQSET_SetManager_SetCurrentSet(_Player, _SetID);
ObjectSetFlag(_Player, "LLEQSET_Menu_CurrentSetEquipped");

PROC
LeaderLib_SetManager_Menu_SetEmptyCurrentSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _CurrentSetID)
AND
_SetID == _CurrentSetID
THEN
ObjectSetFlag(_Player, "LLEQSET_Menu_CurrentSetEquipped");

PROC
LeaderLib_SetManager_Menu_SetEmptyCurrentSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _CurrentSetID)
AND
_SetID != _CurrentSetID
THEN
ObjectClearFlag(_Player, "LLEQSET_Menu_CurrentSetEquipped");

IF
ObjectFlagSet("LLEQSET_Actions_ClearSelectedSetEquipment", (CHARACTERGUID)_Player, _Instance)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Actions_ClearSelectedSetEquipment")
AND
DB_LLEQSET_Menu_SelectedSet(_Player, _SetID)
THEN
LLEQSET_SetManager_ClearSet(_Player, _SetID);
LeaderLib_SetManager_Menu_ClearEquippedSetIfMatched(_Player, _SetID);
ObjectClearFlag(_Player, "LLEQSET_Menu_CurrentSetSaved");

PROC
LeaderLib_SetManager_Menu_ClearEquippedSetIfMatched((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
THEN
ObjectClearFlag(_Player, "LLEQSET_Menu_CurrentSetEquipped");

IF
ObjectFlagSet("LLEQSET_Actions_GiveSelectedSetSkill", (CHARACTERGUID)_Player, _Instance)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Actions_GiveSelectedSetSkill")
AND
DB_LLEQSET_Menu_SelectedSet(_Player, _SetID)
AND
DB_LLEQSET_SetManager_SetConfig(_SetID, _Skill, _ItemTemplate, _BackpackTemplate)
THEN
CharacterAddSkill(_Player, _Skill);

IF
ObjectFlagSet("LLEQSET_Actions_GiveSelectedSetItem", (CHARACTERGUID)_Player, _Instance)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Actions_GiveSelectedSetItem")
AND
DB_LLEQSET_Menu_SelectedSet(_Player, _SetID)
AND
DB_LLEQSET_SetManager_SetConfig(_SetID, _Skill, _ItemTemplate, _BackpackTemplate)
THEN
ItemTemplateAddTo(_ItemTemplate, _Player, 1);
//END_REGION

//REGION FLAG_COMMANDS
IF
ObjectFlagSet("LLEQSET_Actions_GiveAllSetSkills", (CHARACTERGUID)_Player, _Instance)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Actions_GiveAllSetSkills")
AND
DB_LLEQSET_SetManager_SetConfig(_SetID, _Skill, _ItemTemplate, _BackpackTemplate)
THEN
CharacterAddSkill(_Player, _Skill);

IF
ObjectFlagSet("LLEQSET_Actions_GiveAllSetItems", (CHARACTERGUID)_Player, _Instance)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Actions_GiveAllSetItems")
AND
DB_LLEQSET_SetManager_SetConfig(_SetID, _Skill, _ItemTemplate, _BackpackTemplate)
AND
CharacterGetItemTemplateCount(_Player, _ItemTemplate, _Count)
AND
_Count == 0
THEN
ItemTemplateAddTo(_ItemTemplate, _Player, 1);

IF
ObjectFlagSet("LLEQSET_Actions_RemoveAllSetSkills", (CHARACTERGUID)_Player, _Instance)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Actions_RemoveAllSetSkills")
AND
DB_LLEQSET_SetManager_SetConfig(_SetID, _Skill, _ItemTemplate, _BackpackTemplate)
AND
CharacterHasSkill(_Player, _Skill, 1)
THEN
CharacterRemoveSkill(_Player, _Skill);

IF
ObjectFlagSet("LLEQSET_Actions_ClearAllSets", (CHARACTERGUID)_Player, _Instance)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Actions_ClearAllSets")
AND
DB_LLEQSET_SetManager_SetConfig(_SetID, _Skill, _ItemTemplate, _BackpackTemplate)
THEN
LLEQSET_SetManager_ClearSet(_Player, _SetID);

IF
ObjectFlagSet("LLEQSET_Actions_ClearAllSetNames", (CHARACTERGUID)_Player, _Instance)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Actions_ClearAllSetNames")
AND
DB_LLEQSET_SetManager_SetNames(_Player, _SetID, _Name)
THEN
NOT DB_LLEQSET_SetManager_SetNames(_Player, _SetID, _Name);

IF
ObjectFlagSet("LLEQSET_Actions_UnequipEverything", (CHARACTERGUID)_Player, _Instance)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Actions_UnequipEverything")
AND
LLEQSET_QRY_ColorStatusText(_Player, "Unequipped all items.", "UnequippedAll")
AND
DB_LeaderLib_EquipmentSlots(_Slot)
THEN
LLEQSET_SetManager_UnequipItemInSlot_NoDelay(_Player, _Slot);

IF
ObjectFlagSet("LLEQSET_Actions_ReInitialize", (CHARACTERGUID)_Player, _Instance)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Actions_ReInitialize")
THEN
LLEQSET_System_ClearInitialization(_Player);
LLEQSET_System_InitialSetup(_Player);
UserClearFlag(_Player, "LLEQSET_Menu_FirstTimeSetupComplete", 0);
LLEQSET_ColorStatusText(_Player, "Re-Initialized Player", "Warning");

IF
ObjectFlagSet("LLEQSET_Actions_GiveMenuBook", (CHARACTERGUID)_Player, _Instance)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Actions_GiveMenuBook")
AND
DB_LLEQSET_MenuItem(_Template)
THEN
ItemTemplateAddTo(_Template, _Player, 1);
LLEQSET_ColorStatusText(_Player, "Menu book added.", "FriendlyText");

IF
ObjectFlagSet("LLEQSET_Menu_HasMenuSkill", (CHARACTERGUID)_Player, _Instance)
AND
DB_LLEQSET_MenuSkill(_Skill)
AND
CharacterHasSkill(_Player, _Skill, 0)
THEN
CharacterAddSkill(_Player, _Skill);

IF
ObjectFlagCleared("LLEQSET_Menu_HasMenuSkill", (CHARACTERGUID)_Player, _Instance)
AND
DB_LLEQSET_MenuSkill(_Skill)
AND
CharacterHasSkill(_Player, _Skill, 1)
THEN
CharacterRemoveSkill(_Player, _Skill);
//END_REGION

//REGION MENU_OPENING
PROC
LLEQSET_Menu_OpenMenu((CHARACTERGUID)_Player)
AND
CharacterIsInCombat(_Player, _CombatState)
THEN
LLEQSET_Menu_OpenMenu((CHARACTERGUID)_Player, _CombatState);

PROC
LLEQSET_Menu_OpenMenu((CHARACTERGUID)_Player, 0)
THEN
Proc_StartDialog(0, "LLEQSET_EquipmentSetMenu", _Player, _Player);

PROC
LLEQSET_Menu_OpenMenu((CHARACTERGUID)_Player, 1)
THEN
LLEQSET_ColorStatusText(_Player, "Unable to open the Equipment Set menu while in combat!", "Error");

//Events
IF
CharacterUsedSkill(_Player, _Skill, _, _)
AND
DB_LLEQSET_MenuSkill(_Skill)
THEN
LLEQSET_Menu_OpenMenu(_Player);

IF
CharacterUsedItemTemplate(_Player, _Template, _Item)
AND
DB_LLEQSET_MenuItem(_Template)
THEN
LLEQSET_Menu_OpenMenu(_Player);
//END_REGION

//REGION FLAG_EVENTS
IF
ObjectFlagSet("LLEQSET_Settings_AutoSaveEnabled", (CHARACTERGUID)_Player, _Instance)
AND
DB_LLEQSET_SetManager_Temp_ActiveDialog(_Player, _Instance)
THEN
LLEQSET_ColorStatusText(_Player, "[Equipment Sets] Auto-Save Enabled", "SetSaved");

IF
ObjectFlagCleared("LLEQSET_Settings_AutoSaveEnabled", (CHARACTERGUID)_Player, _Instance)
AND
DB_LLEQSET_SetManager_Temp_ActiveDialog(_Player, _Instance)
THEN
LLEQSET_ColorStatusText(_Player, "[Equipment Sets] Auto-Save Disabled", "Warning");
//END_REGION

//REGION DIALOG_EVENTS
IF
DialogStarted("LLEQSET_EquipmentSetMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
THEN
LLEQSET_Menu_LoadSetNames(_Player, _Instance);
LLEQSET_Menu_SetActiveSetNames(_Player, _Instance);
LLEQSET_Menu_InitMenuFlags(_Player, _Instance);
DB_LLEQSET_SetManager_Temp_ActiveDialog(_Player, _Instance);

IF
DialogEnded("LLEQSET_EquipmentSetMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
THEN
LLEQSET_Menu_ClearMenuFlags(_Player);
NOT DB_LLEQSET_SetManager_Temp_ActiveDialog(_Player, _Instance);
//END_REGION

//REGION NAMING
IF
ObjectFlagSet("LLEQSET_Menu_ConfirmName", (CHARACTERGUID)_Player, _Instance)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Menu_ConfirmName")
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Naming_TextSaved")
AND
DB_LLEQSET_Naming_FinishedText(_Name, _Player)
AND
DB_LLEQSET_Menu_SelectedSet(_Player, _SetID)
THEN
LLEQSET_Menu_ClearSetName(_Player, _SetID);
LLEQSET_SetManager_RegisterSetName(_Player, _SetID, _Name);
LLEQSET_Menu_LoadSetNames(_Player, _Instance);
LLEQSET_Menu_SetCurrentSetName(_Player, _Instance, _SetID);
NOT DB_LLEQSET_Naming_FinishedText(_Name, _Player);

IF
ObjectFlagSet("LLEQSET_Menu_CancelName", (CHARACTERGUID)_Player, _Instance)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Menu_CancelName")
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Naming_TextSaved")
AND
DB_LLEQSET_Naming_FinishedText(_Name, _Player)
THEN
NOT DB_LLEQSET_Naming_FinishedText(_Name, _Player);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_EquipmentSets"
