Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_LLEQSET_Menu_SelectedSet(_Player, _SetID)
//DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
//DB_LLEQSET_SetManager_LastSet(_Player, _SetID)
//DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, _Item)
//DB_LLEQSET_SetManager_SetNames(_Player, _SetID, _Name)
//DB_LLEQSET_SetManager_SetConfig(_SetID, _Skill, _ItemTemplate, _BackpackTemplate)
//DB_LLEQSET_SetManager_Variables(_SetID, _DialogNameVar, _SelectFlag, _EquipSetFlag

//DB_LLEQSET_SetManager_EquipRate(_TickRate)

//DB_LLEQSET_Temp_SetManager_DisableAutoSave(_Player)
//DB_LLEQSET_Temp_SetManager_UnequippedSet(_Player, _SetID)
//DB_LLEQSET_Temp_SetManager_UnequippedItemInSet(_Player, _SetID)

//DB_LLEQSET_SetManager_BackpackContents(_Player, _Backpack, _Item)
/*EQUIP_TIMER*/
//DB_LLEQSET_Temp_EquipTimer(_Player, _SetID, _TimerName, _CurrentIndex, _Mode)
//DB_LLEQSET_Temp_EquipTimerActive(_Player, _TimerName)
//Type setting
DB_LLEQSET_SavedSets((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000, "", "", (ITEMGUID)NULL_00000000-0000-0000-0000-000000000000);

LLEQSET_SetManager_InitializeSettings();
KBSECTION
//REGION SETTINGS
PROC
LLEQSET_SetManager_InitializeSettings()
THEN
LLEQSET_System_ClearExistingDatabase("DB_LLEQSET_SetManager_SetConfig", 4);
DB_LLEQSET_SetManager_SetConfig("Set1", "Shout_LLEQSET_EquipmentSet1", "LLEQSET_SetSwitcher_Set1_0730aca7-6ffd-4206-a0bf-6c454d0017f1", "LLEQSET_BackPack_Set1_6ba54120-1265-4827-a96f-7ca658a569c4");
DB_LLEQSET_SetManager_SetConfig("Set2", "Shout_LLEQSET_EquipmentSet2", "LLEQSET_SetSwitcher_Set2_39f4f924-dd09-42d3-9da2-9dcb852f4d6b", "LLEQSET_BackPack_Set2_4193c597-b722-4772-85f1-f96ddd82843f");
DB_LLEQSET_SetManager_SetConfig("Set3", "Shout_LLEQSET_EquipmentSet3", "LLEQSET_SetSwitcher_Set3_b3fd8c1b-caba-4c5e-bb71-9d85d684b17a", "LLEQSET_BackPack_Set3_8b410fe7-7a27-4e4f-bcc1-39857b385120");
DB_LLEQSET_SetManager_SetConfig("Set4", "Shout_LLEQSET_EquipmentSet4", "LLEQSET_SetSwitcher_Set4_771ba282-d742-499b-8709-73a4239a5c29", "LLEQSET_BackPack_Set4_57d6b94d-a6fb-42b5-9b54-cb25ab8dbf97");
DB_LLEQSET_SetManager_SetConfig("Set5", "Shout_LLEQSET_EquipmentSet5", "LLEQSET_SetSwitcher_Set5_fc91f52e-978b-454e-8ac2-ddb81d91601b", "LLEQSET_BackPack_Set5_600b5f95-6389-4c75-80bd-bb496e5b8143");

NOT DB_LLEQSET_SetManager_SavedSetEquipment((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000, "", "", (ITEMGUID)NULL_00000000-0000-0000-0000-000000000000);

LLEQSET_System_ClearExistingDatabase("DB_LLEQSET_SetManager_Variables", 4);
DB_LLEQSET_SetManager_Variables("Set1", "LLEQSET_Set1_Name_66186e4d-bc09-41b6-892f-15da2693d668", "LLEQSET_Actions_SelectSet1", "LLEQSET_Actions_EquipSet1");
DB_LLEQSET_SetManager_Variables("Set2", "LLEQSET_Set2_Name_cd6de903-2364-4c6d-9a10-9b2b2707236d", "LLEQSET_Actions_SelectSet2", "LLEQSET_Actions_EquipSet2");
DB_LLEQSET_SetManager_Variables("Set3", "LLEQSET_Set3_Name_11964073-abcf-4d8e-b509-7cfe4fe42a7a", "LLEQSET_Actions_SelectSet3", "LLEQSET_Actions_EquipSet3");
DB_LLEQSET_SetManager_Variables("Set4", "LLEQSET_Set4_Name_b099c556-cd9b-4764-b8e8-21151e9d0d73", "LLEQSET_Actions_SelectSet4", "LLEQSET_Actions_EquipSet4");
DB_LLEQSET_SetManager_Variables("Set5", "LLEQSET_Set5_Name_96b8dacd-3eaf-4b43-bb88-ea1af58840e2", "LLEQSET_Actions_SelectSet5", "LLEQSET_Actions_EquipSet5");

LLEQSET_System_ClearExistingDatabase("DB_LLEQSET_SetManager_EquipRate", 1);
DB_LLEQSET_SetManager_EquipRate(100);

PROC
LLEQSET_System_UpdateDatabases()
THEN
LLEQSET_SetManager_InitializeSettings();

PROC
LLEQSET_System_RegisterDatabases()
THEN
LeaderUpdater_Register_Database("EquipmentSets", "LaughingLeader", "SetManager_Main", "DB_LLEQSET_Menu_SelectedSet", 2);
LeaderUpdater_Register_Database("EquipmentSets", "LaughingLeader", "SetManager_Main", "DB_LLEQSET_SetManager_CurrentSet", 2);
LeaderUpdater_Register_Database("EquipmentSets", "LaughingLeader", "SetManager_Main", "DB_LLEQSET_SetManager_LastSet", 2);
LeaderUpdater_Register_Database("EquipmentSets", "LaughingLeader", "SetManager_Main", "DB_LLEQSET_SetManager_SavedSetEquipment", 4);
LeaderUpdater_Register_Database("EquipmentSets", "LaughingLeader", "SetManager_Main", "DB_LLEQSET_SetManager_SetNames", 3);
LeaderUpdater_Register_Database("EquipmentSets", "LaughingLeader", "SetManager_Main", "DB_LLEQSET_SetManager_SetConfig", 4);
LeaderUpdater_Register_Database("EquipmentSets", "LaughingLeader", "SetManager_Main", "DB_LLEQSET_SetManager_Variables", 4);
LeaderUpdater_Register_Database("EquipmentSets", "LaughingLeader", "SetManager_Main", "DB_LLEQSET_SetManager_EquipRate", 1);
LeaderUpdater_Register_Database("EquipmentSets", "LaughingLeader", "SetManager_Main", "DB_LLEQSET_Temp_EquipTimer", 5);
LeaderUpdater_Register_Database("EquipmentSets", "LaughingLeader", "SetManager_Main", "DB_LLEQSET_Temp_EquipTimerActive", 2);
LeaderLog_Log("DEBUG","[LLEQSET:SetManager:Main] Databases registered.");
//END_REGION

//REGION CLEAR
PROC
LLEQSET_SetManager_ClearSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
LLEQSET_QRY_SetNotEmpty(_Player, _SetID)
AND
LLEQSET_QRY_Helpers_GetSetName(_Player, _SetID)
AND
DB_LLEQSET_Temp_SetName(_Player, _SetID, _SetName)
AND
StringConcatenate(_SetName, " cleared.", _Message)
THEN
LLEQSET_ColorStatusText(_Player, _Message, "SetCleared");
NOT DB_LLEQSET_Temp_SetName(_Player, _SetID, _SetName);

PROC
LLEQSET_SetManager_ClearSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, _Item)
THEN
NOT DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, _Item);
LLEQSET_SetManager_RemoveFromBackpack(_Player, _SetID, _Item);

PROC
LLEQSET_SetManager_ClearSavedInSlot((CHARACTERGUID)_Player, (STRING)_SetID, (STRING)_Slot)
AND
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, _Item)
THEN
NOT DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, _Item);
LLEQSET_SetManager_RemoveFromBackpack(_Player, _SetID, _Item);
//END_REGION

//REGION PLAYER_QUERIES
QRY
LLEQSET_QRY_PlayerHasEquipment((CHARACTERGUID)_Player)
AND
DB_LLLIB_EquipmentSlots(_Slot)
AND
CharacterGetEquippedItem(_Player, _Slot, _EquippedItem)
AND
_EquippedItem != NULL_00000000-0000-0000-0000-000000000000
THEN
DB_NOOP(1);

QRY
LLEQSET_QRY_ItemNotEquippedInSlot((CHARACTERGUID)_Player, (STRING)_Slot, (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
NOT CharacterGetEquippedItem(_Player, _Slot, _)
THEN
DB_NOOP(1);

QRY
LLEQSET_QRY_ItemNotEquippedInSlot((CHARACTERGUID)_Player, (STRING)_Slot, (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
CharacterGetEquippedItem(_Player, _Slot, _CurrentItem)
AND
_CurrentItem != _Item
THEN
DB_NOOP(1);

QRY
LLEQSET_QRY_ItemNotEquippedInSlot((CHARACTERGUID)_Player, (STRING)_Slot, (ITEMGUID)_Item)
AND
_Item == NULL_00000000-0000-0000-0000-000000000000
AND
ObjectGetFlag(_Player, "LLEQSET_Settings_SaveEmptySlots", 1)
AND
CharacterGetEquippedItem(_Player, _Slot, _CurrentItem)
THEN
DB_NOOP(1);

QRY
LLEQSET_QRY_PlayerEquipTimerActive((CHARACTERGUID)_Player)
AND
DB_LLEQSET_Temp_EquipTimer(_Player, _SetID, _TimerName, _CurrentIndex, _Mode)
THEN
DB_NOOP(1);

QRY
LLEQSET_QRY_PlayerEquipTimerActive((CHARACTERGUID)_Player)
AND
DB_LLEQSET_Temp_EquipTimerActive(_Player, _TimerName)
THEN
DB_NOOP(1);
//END_REGION

//REGION SET_QUERIES
QRY
LLEQSET_QRY_SetNotEmpty((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, _Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
THEN
DB_NOOP(1);

QRY
LLEQSET_QRY_SetExists((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_SetManager_SavedSetEquipment(_Player,_SetID,_,_)
THEN
DB_NOOP(1);

QRY
LLEQSET_QRY_SetManager_WillUnequip((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, _Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
CharacterGetEquippedItem(_Player, _Slot, _Item)
THEN
DB_NOOP(1);

QRY
LLEQSET_QRY_SetManager_SetEquipmentMismatch((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, _Item)
AND
LLEQSET_QRY_ItemNotEquippedInSlot(_Player, _Slot, _Item)
THEN
DB_NOOP(1);

/*
QRY
LLEQSET_QRY_SetManager_ShouldChangeSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
LLEQSET_QRY_SetManager_SetEquipmentMismatch(_Player, _SetID)
THEN
DB_NOOP(1);
*/

QRY
LLEQSET_QRY_SetManager_ShouldChangeSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
LLEQSET_QRY_SetExists(_Player, _SetID)
AND
NOT DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
THEN
DB_NOOP(1);

QRY
LLEQSET_QRY_SetManager_ShouldEquipSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
LLEQSET_QRY_SetExists(_Player, _SetID)
AND
LLEQSET_QRY_SetNotEmpty(_Player, _SetID)
THEN
DB_NOOP(1);

QRY
LLEQSET_QRY_SetManager_EquipmentIsChanging((CHARACTERGUID)_Player, (STRING)_NextSetID)
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
AND
_SetID != _NextSetID
AND
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, _Item)
AND
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _NextSetID, _Slot, _NextItem)
AND
_NextItem != _Item
THEN
DB_NOOP(1);
//END_REGION

//REGION BACKPACKS
QRY
LLEQSET_QRY_SetManager_CreateBackpackIfMissing((CHARACTERGUID)_Player, (STRING)_BackpackTemplate)
AND
CharacterGetItemTemplateCount(_Player, _BackpackTemplate, 0)
THEN
ItemTemplateAddTo(_BackpackTemplate, _Player, 1);

QRY
LLEQSET_QRY_SetManager_CreateBackpackIfMissing((CHARACTERGUID)_Player, (STRING)_BackpackTemplate)
AND
CharacterGetItemTemplateCount(_Player, _BackpackTemplate, 1)
THEN
DB_NOOP(1);

QRY
LLEQSET_QRY_GetTargetBackpack((CHARACTERGUID)_Player, (ITEMGUID)_Item)
AND
DB_LLEQSET_Temp_TargetBackpack(_Player, _BackpackTemplate)
THEN
NOT DB_LLEQSET_Temp_TargetBackpack(_Player, _BackpackTemplate);

//Prioritize last set.
QRY
LLEQSET_QRY_GetTargetBackpack((CHARACTERGUID)_Player, (ITEMGUID)_Item)
AND
DB_LLEQSET_SetManager_LastSet(_Player, _SetID)
AND
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, _Item)
AND
DB_LLEQSET_SetManager_SetConfig(_SetID, _Skill, _ItemTemplate, _BackpackTemplate)
THEN
DB_LLEQSET_Temp_TargetBackpack(_Player, _BackpackTemplate);

//Default to the saved set if it doesn't exist in the last set.
QRY
LLEQSET_QRY_GetTargetBackpack((CHARACTERGUID)_Player, (ITEMGUID)_Item)
AND
DB_LLEQSET_SetManager_LastSet(_Player, _SetID)
AND
NOT DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _, _Item)
AND
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _OtherSetID, _Slot, _Item)
AND
DB_LLEQSET_SetManager_SetConfig(_OtherSetID, _Skill, _ItemTemplate, _BackpackTemplate)
THEN
DB_LLEQSET_Temp_TargetBackpack(_Player, _BackpackTemplate);

PROC
LLEQSET_SetManager_StoreInBackpack((CHARACTERGUID)_Player, (ITEMGUID)_Item)
AND
ObjectGetFlag(_Player, "LLEQSET_Settings_StoreInBackpacksDisabled", 0)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
LLEQSET_QRY_GetTargetBackpack(_Player, _Item)
AND
DB_LLEQSET_Temp_TargetBackpack(_Player, _BackpackTemplate)
AND
LLEQSET_QRY_SetManager_CreateBackpackIfMissing(_Player, _BackpackTemplate)
AND
GetItemForItemTemplateInInventory(_Player, _BackpackTemplate, _Backpack)
THEN
ItemToInventory(_Item, _Backpack, 1, 0, 0);
NOT DB_LLEQSET_Temp_TargetBackpack(_Player, _BackpackTemplate);

PROC
LLEQSET_SetManager_RemoveFromBackpack((CHARACTERGUID)_Player, (STRING)_SetID, (ITEMGUID)_Item)
AND
DB_LLEQSET_SetManager_BackpackContents(_Player, _Backpack, _Item)
THEN
ItemToInventory(_Item, _Player, 1, 0, 0);
NOT DB_LLEQSET_SetManager_BackpackContents(_Player, _Backpack, _Item);
LLEQSET_SetManager_RemoveFromBackpack_PlayMessage(_Player);

PROC
LLEQSET_SetManager_RemoveFromBackpack_PlayMessage((CHARACTERGUID)_Player)
AND
NOT DB_LLEQSET_Timers_DelayedMessage(_,_Player,"Previous Set Items Removed from Backpack",_)
AND
GetUUID(_Player, _ID)
AND
StringConcatenate("LLEQSET_Timers_SetManager_BackpackItemRemoval_", _ID, _TimerName)
THEN
DB_LLEQSET_Timers_DelayedMessage(_TimerName, _Player,"Previous Set Items Removed from Backpack", "Warning");
TimerLaunch(_TimerName, 500);
//END_REGION

//REGION HELPERS
QRY
LLEQSET_QRY_SetManager_SetCurrentSet((CHARACTERGUID)_Player, (STRING)_SetID)
THEN
LLEQSET_SetManager_SetCurrentSet(_Player, _SetID);

PROC
LLEQSET_SetManager_SetCurrentSet((CHARACTERGUID)_Player, (STRING)_NextSetID)
AND
NOT DB_LLEQSET_SetManager_LastSet(_Player, _)
THEN
LLEQSET_SetManager_SetLastSet(_Player, _NextSetID);

PROC
LLEQSET_SetManager_SetCurrentSet((CHARACTERGUID)_Player, (STRING)_NextSetID)
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
THEN
LLEQSET_SetManager_SetLastSet(_Player, _SetID);
NOT DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID);

PROC
LLEQSET_SetManager_SetCurrentSet((CHARACTERGUID)_Player, (STRING)_SetID)
THEN
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID);

PROC
LLEQSET_SetManager_SetLastSet((CHARACTERGUID)_Player, (STRING)_NextSetID)
AND
DB_LLEQSET_SetManager_LastSet(_Player, _SetID)
THEN
NOT DB_LLEQSET_SetManager_LastSet(_Player, _SetID);

PROC
LLEQSET_SetManager_SetLastSet((CHARACTERGUID)_Player, (STRING)_SetID)
THEN
DB_LLEQSET_SetManager_LastSet(_Player, _SetID);

PROC
LLEQSET_SetManager_ClearCurrentSet((CHARACTERGUID)_Player)
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
THEN
LLEQSET_SetManager_SetLastSet(_Player, _SetID);
NOT DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID);

PROC
LLEQSET_SetManager_RegisterSetName((CHARACTERGUID)_Player, (STRING)_SetID, (STRING)_Name)
AND
DB_LLEQSET_SetManager_SetNames(_Player, _SetID, _CurrentName)
THEN
NOT DB_LLEQSET_SetManager_SetNames(_Player, _SetID, _CurrentName);

PROC
LLEQSET_SetManager_RegisterSetName((CHARACTERGUID)_Player, (STRING)_SetID, (STRING)_Name)
THEN
DB_LLEQSET_SetManager_SetNames(_Player, _SetID, _Name);
//END_REGION

//REGION EQUIP_ITERATION
PROC
LLEQSET_SetManager_Internal_StartEquipTimer((CHARACTERGUID)_Player, (STRING)_SetID, (INTEGER)_Mode)
AND
NOT DB_LLEQSET_Temp_EquipTimer(_Player, _SetID, (STRING)_, (INTEGER)_, (INTEGER)_)
AND
String(_Player, _ID)
AND
LeaderLog_QRY_Log("COMBINE", "LLEQSET_Timers_", _ID, "_", _SetID, "_EquipTimer")
AND
DB_LeaderLog_Temp_CombinedString(_TimerName)
THEN
NOT DB_LeaderLog_Temp_CombinedString(_TimerName);
DB_LLEQSET_Temp_EquipTimer(_Player, _SetID, _TimerName, 0, _Mode);
LLEQSET_SetManager_Internal_OnEquipTick(_Player, _SetID, 0, _Mode);
LLEQSET_SetManager_Internal_NextEquipTick(_Player, _SetID);

PROC
LLEQSET_SetManager_Internal_OnEquipTick((CHARACTERGUID)_Player, (STRING)_SetID, (INTEGER)_SlotIndex, (INTEGER)_Mode)
AND
_Mode > 0
AND
DB_LLLIB_Array_Data("LeaderLib_EquipmentSlots", _SlotIndex, _Slot)
AND
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, _Item)
AND
LLEQSET_QRY_ItemNotEquippedInSlot(_Player, _Slot, _Item)
THEN
LLEQSET_SetManager_EquipItem_NoDelay(_Player, _Slot, _Item);

PROC
LLEQSET_SetManager_Internal_OnEquipTick((CHARACTERGUID)_Player, (STRING)_SetID, (INTEGER)_SlotIndex, (INTEGER)_Mode)
AND
_Mode <= 0
AND
DB_LLLIB_Array_Data("LeaderLib_EquipmentSlots", _SlotIndex, _Slot)
AND
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, _Item)
THEN
LLEQSET_SetManager_UnequipItemInSlot_NoDelay(_Player, _Slot);

PROC
LLEQSET_SetManager_Internal_NextEquipTick((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_Temp_EquipTimer(_Player, _SetID, _TimerName, _CurrentIndex, _Mode)
AND
DB_LLLIB_Array_Length("LeaderLib_EquipmentSlots", _Length)
AND
_CurrentIndex >= _Length
THEN
NOT DB_LLEQSET_Temp_EquipTimer(_Player, _SetID, _TimerName, _CurrentIndex, _Mode);
LLEQSET_SetManager_Internal_EquipTimerFinished(_Player, _SetID, _Mode);

PROC
LLEQSET_SetManager_Internal_EquipTimerFinished((CHARACTERGUID)_Player, (STRING)_SetID, (INTEGER)_Mode)
AND
_Mode > 0
THEN
SetStoryEvent(_Player, "EquipmentSets_OnSetEquipped");

PROC
LLEQSET_SetManager_Internal_EquipTimerFinished((CHARACTERGUID)_Player, (STRING)_SetID, (INTEGER)_Mode)
AND
_Mode <= 0
THEN
SetStoryEvent(_Player, "EquipmentSets_OnSetUnequipped");

PROC
LLEQSET_SetManager_Internal_NextEquipTick((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_Temp_EquipTimer(_Player, _SetID, _TimerName, _CurrentIndex, _Mode)
AND
DB_LLLIB_Array_Length("LeaderLib_EquipmentSlots", _Length)
AND
_CurrentIndex < _Length
AND
IntegerSum(_CurrentIndex, 1, _NextIndex)
THEN
NOT DB_LLEQSET_Temp_EquipTimer(_Player, _SetID, _TimerName, _CurrentIndex, _Mode);
DB_LLEQSET_Temp_EquipTimer(_Player, _SetID, _TimerName, _NextIndex, _Mode);

PROC
LLEQSET_SetManager_Internal_NextEquipTick((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_Temp_EquipTimer(_Player, _SetID, _TimerName, _CurrentIndex, _Mode)
AND
NOT DB_LLEQSET_Temp_EquipTimerActive(_Player, _TimerName)
AND
DB_LLEQSET_SetManager_EquipRate(_TickRate)
THEN
DB_LLEQSET_Temp_EquipTimerActive(_Player, _TimerName);
TimerLaunch(_TimerName, _TickRate);

IF
TimerFinished(_TimerName)
AND
DB_LLEQSET_Temp_EquipTimerActive(_Player, _TimerName)
THEN
NOT DB_LLEQSET_Temp_EquipTimerActive(_Player, _TimerName);

IF
TimerFinished(_TimerName)
AND
DB_LLEQSET_Temp_EquipTimer(_Player, _SetID, _TimerName, _Index, _Mode)
THEN
LLEQSET_SetManager_Internal_OnEquipTick(_Player, _SetID, _Index, _Mode);
LLEQSET_SetManager_Internal_NextEquipTick(_Player, _SetID);
//END_REGION

//REGION EQUIPPING
IF
StoryEvent((CHARACTERGUID)_Player, "EquipmentSets_OnSetEquipped")
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
THEN
LLEQSET_ColorSetText(_Player, _SetID, "SetChanged", "Equipment Set changed to  ", "");

PROC
LLEQSET_SetManager_EquipItem_NoDelay((CHARACTERGUID)_Player, (STRING)_Slot, (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
ItemIsInCharacterInventory(_Item, _Player, 0)
AND
StringConcatenate("Missing item for slot ", _Slot, _Message)
THEN
LLEQSET_ColorStatusText(_Player, _Message, "Error");

PROC
LLEQSET_SetManager_EquipItem_NoDelay((CHARACTERGUID)_Player, (STRING)_Slot, (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
ItemIsInCharacterInventory(_Item, _Player, 1)
THEN
LLEQSET_SetManager_UnequipItemInSlot_NoDelay(_Player, _Slot);
CharacterEquipItem(_Player, _Item);

PROC
LLEQSET_SetManager_EquipItem_NoDelay((CHARACTERGUID)_Player, (STRING)_Slot, (ITEMGUID)_Item)
AND
_Item == NULL_00000000-0000-0000-0000-000000000000
AND
ObjectGetFlag(_Player, "LLEQSET_Settings_SaveEmptySlots", 1)
THEN
LLEQSET_SetManager_UnequipItemInSlot_NoDelay(_Player, _Slot);
//END_REGION

//REGION UNEQUIPPING
IF
StoryEvent((CHARACTERGUID)_Player, "EquipmentSets_OnSetUnequipped")
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
THEN
LLEQSET_SetManager_ClearCurrentSet(_Player);
LLEQSET_ColorStatusText(_Player, "Current Set: None", "Warning");

PROC
LLEQSET_SetManager_UnequipItemInSlot_NoDelay((CHARACTERGUID)_Player, (STRING)_Slot)
AND
CharacterGetEquippedItem(_Player, _Slot, (ITEMGUID)_Item)
THEN
LLEQSET_SetManager_Internal_UnequipItem(_Player, _Item);

PROC
LLEQSET_SetManager_Internal_UnequipItem((CHARACTERGUID)_Player, (ITEMGUID)_Item)
THEN
CharacterUnequipItem(_Player, _Item);
LLEQSET_SetManager_StoreInBackpack(_Player, _Item);

PROC
LLEQSET_SetManager_UnequipSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, _Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
THEN
LLEQSET_SetManager_Internal_SetHasUnequippedItem(_Player, _SetID);
LLEQSET_SetManager_UnequipItemInSlot_NoDelay(_Player, _Slot);

PROC
LLEQSET_SetManager_Internal_SetHasUnequippedItem((CHARACTERGUID)_Player, (STRING)_SetID)
AND
NOT DB_LLEQSET_Temp_SetManager_UnequippedItemInSet(_Player, _SetID)
THEN
DB_LLEQSET_Temp_SetManager_UnequippedItemInSet(_Player, _SetID);

PROC
LLEQSET_SetManager_UnequipSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
LLEQSET_QRY_SetExists(_Player, _SetID)
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
AND
DB_LLEQSET_Temp_SetManager_UnequippedItemInSet(_Player, _SetID)
THEN
LLEQSET_ColorSetText(_Player, _SetID, "UnequippedAll", "Unequipped all equipment in ", "");
NOT DB_LLEQSET_Temp_SetManager_UnequippedItemInSet(_Player, _SetID);

PROC
LLEQSET_SetManager_UnequipSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
LLEQSET_QRY_SetExists(_Player, _SetID)
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
THEN
LLEQSET_SetManager_ClearCurrentSet(_Player);
LLEQSET_ColorStatusText(_Player, "Current Set: None", "Warning");
//END_REGION

//REGION CHANGE_SET
PROC
LLEQSET_SetManager_ChangeSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
ObjectGetFlag(_Player, "LLEQSET_Settings_AutoSaveEnabled", 1)
THEN
DB_LLEQSET_Temp_SetManager_DisableAutoSave(_Player);

PROC
LLEQSET_SetManager_ChangeSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
LLEQSET_QRY_SetExists(_Player,_SetID)
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
THEN
LLEQSET_SetManager_UnequipSet(_Player, _SetID);
//LLEQSET_SetManager_Internal_StartEquipTimer(_Player, _SetID, 0);
DB_LLEQSET_Temp_SetManager_UnequippedSet(_Player, _SetID);

PROC
LLEQSET_SetManager_ChangeSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
LLEQSET_QRY_SetExists(_Player,_SetID)
AND
LLEQSET_QRY_SetManager_ShouldChangeSet(_Player, _SetID)
AND
NOT DB_LLEQSET_Temp_SetManager_UnequippedSet(_Player, _SetID)
THEN
LLEQSET_SetManager_Internal_OverwriteEmptySlotsInNextSet(_Player, _SetID);
LLEQSET_SetManager_SetCurrentSet(_Player, _SetID);
LLEQSET_SetManager_Internal_StartEquipTimer(_Player, _SetID, 1);

//If auto-save is enabled, fill empty slots in the next set with currently equipped items
PROC
LLEQSET_SetManager_Internal_OverwriteEmptySlotsInNextSet((CHARACTERGUID)_Player, (STRING)_NextSetID)
AND
ObjectGetFlag(_Player, "LLEQSET_Settings_AutoSaveEnabled", 1)
AND
ObjectGetFlag(_Player, "LLEQSET_Settings_SaveEmptySlots", 0)
AND
DB_LLLIB_EquipmentSlots(_Slot)
AND
NOT DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _NextSetID, _Slot, _)
AND
CharacterGetEquippedItem(_Player, _Slot, (ITEMGUID)_Item)
THEN
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _NextSetID, _Slot, _Item);

/*
PROC
LLEQSET_SetManager_ChangeSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
LLEQSET_QRY_SetExists(_Player,_SetID)
AND
NOT LLEQSET_QRY_SetNotEmpty(_Player,_SetID) // Set is empty
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
THEN
LLEQSET_SetManager_RegisterAllEquipment(_Player, _SetID);
*/

QRY
LLEQSET_QRY_SetManager_CanSaveEquipment((CHARACTERGUID)_Player, (STRING)_SetID)
AND
NOT LLEQSET_QRY_SetExists(_Player,_SetID)
AND
LLEQSET_QRY_PlayerHasEquipment(_Player)
THEN
DB_NOOP(1);

QRY
LLEQSET_QRY_SetManager_CanSaveEquipment((CHARACTERGUID)_Player, (STRING)_SetID)
AND
NOT LLEQSET_QRY_SetExists(_Player,_SetID)
AND
NOT LLEQSET_QRY_PlayerHasEquipment(_Player)
AND
ObjectGetFlag(_Player, "LLEQSET_Settings_SaveEmptySlots", 1)
THEN
DB_NOOP(1);

PROC
LLEQSET_SetManager_ChangeSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
LLEQSET_QRY_SetManager_CanSaveEquipment(_Player,_SetID)
THEN
LLEQSET_SetManager_RegisterAllEquipment(_Player, _SetID);
LLEQSET_SetManager_SetCurrentSet(_Player, _SetID);
LLEQSET_ColorSetText(_Player, _SetID, "SetChanged", "Equipment Set changed to  ", "");

PROC
LLEQSET_SetManager_ChangeSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
NOT LLEQSET_QRY_SetExists(_Player,_SetID)
AND
NOT LLEQSET_QRY_PlayerHasEquipment(_Player)
AND
ObjectGetFlag(_Player, "LLEQSET_Settings_SaveEmptySlots", 0)
THEN
LLEQSET_ColorStatusText(_Player, "You're completely naked, and this set has not been saved!", "Error");

PROC
LLEQSET_SetManager_RegisterAllEquipment((CHARACTERGUID)_Player, (STRING)_SetID)
AND
LLEQSET_QRY_SetExists(_Player,_SetID)
THEN
LLEQSET_SetManager_ClearSet(_Player, _SetID);

PROC
LLEQSET_SetManager_RegisterAllEquipment((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLLIB_EquipmentSlots(_Slot)
AND
CharacterGetEquippedItem(_Player, _Slot, (ITEMGUID)_Item)
THEN
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, _Item);

PROC
LLEQSET_SetManager_RegisterAllEquipment((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLLIB_EquipmentSlots(_Slot)
AND
NOT CharacterGetEquippedItem(_Player, _Slot, _)
THEN
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, NULL_00000000-0000-0000-0000-000000000000);

PROC
LLEQSET_SetManager_RegisterAllEquipment((CHARACTERGUID)_Player, (STRING)_SetID)
THEN
LLEQSET_ColorSetText(_Player, _SetID, "SetSaved", "Saved all current equipment to   ", "");

PROC
LLEQSET_SetManager_RegisterEquipment((CHARACTERGUID)_Player, (STRING)_SetID, (STRING)_Slot, (ITEMGUID)_Item)
AND
LLEQSET_QRY_SetExists(_Player,_SetID)
THEN
LLEQSET_SetManager_ClearSavedInSlot(_Player, _SetID, _Slot);
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, _Item);

PROC
LLEQSET_SetManager_RegisterEquipment((CHARACTERGUID)_Player, (STRING)_SetID, (STRING)_Slot, (ITEMGUID)_Item)
AND
NOT LLEQSET_QRY_SetExists(_Player,_SetID)
AND
DB_LLEQSET_SetManager_SetConfig(_SetID,_,_,_)
THEN
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, _Item);

//END_REGION

//REGION FLAG_EVENTS
IF
ObjectFlagSet("LLEQSET_Actions_UnequipCurrentSet", (CHARACTERGUID)_Player, _Instance)
AND
LLEQSET_QRY_ClearObjectFlag(_Player, "LLEQSET_Actions_UnequipCurrentSet")
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
THEN
LLEQSET_SetManager_UnequipSet(_Player, _SetID);
//END_REGION

//REGION USE_EVENTS
IF
CharacterUsedSkill(_Player, _Skill, _)
AND
DB_LLEQSET_SetManager_SetConfig(_SetID, _Skill,_,_)
AND
LLEQSET_QRY_SetManager_ShouldPlayEffect(_Player, _SetID)
THEN
LLEQSET_SetManager_PlayOverlayEffect(_Player, _SetID);

IF
SkillCast(_Player, _Skill, _)
AND
DB_LLEQSET_SetManager_SetConfig(_SetID, _Skill,_,_)
AND
LLEQSET_QRY_SetManager_ShouldPlayEffect(_Player, _SetID)
THEN
LLEQSET_SetManager_PlayCastEffect(_Player, _SetID);

IF
SkillCast(_Player, _Skill, _)
AND
DB_LLEQSET_SetManager_SetConfig(_SetID, _Skill,_,_)
THEN
LLEQSET_SetManager_ChangeSet(_Player, _SetID);

IF
CharacterUsedItemTemplate(_Player, _ItemTemplate, _Item)
AND
DB_LLEQSET_SetManager_SetConfig(_SetID, _Skill, _ItemTemplate, _BackpackTemplate)
AND
LLEQSET_QRY_SetManager_ShouldChangeSet(_Player, _SetID)
AND
LLEQSET_QRY_SetManager_ShouldPlayEffect(_Player, _SetID)
THEN
LLEQSET_SetManager_PlayOverlayEffect(_Player, _SetID);
LLEQSET_SetManager_PlayCastEffect(_Player, _SetID);

IF
CharacterUsedItemTemplate(_Player, _ItemTemplate, _Item)
AND
DB_LLEQSET_SetManager_SetConfig(_SetID, _Skill, _ItemTemplate, _BackpackTemplate)
AND
NOT LLEQSET_QRY_SetManager_ShouldChangeSet(_Player, _SetID)
AND
LLEQSET_QRY_SetManager_ShouldPlayEffect(_Player, _SetID)
THEN
LLEQSET_SetManager_PlayOverlayEffect(_Player, _SetID);

IF
CharacterUsedItemTemplate(_Player, _ItemTemplate, _Item)
AND
DB_LLEQSET_SetManager_SetConfig(_SetID, _Skill, _ItemTemplate, _BackpackTemplate)
THEN
LLEQSET_SetManager_ChangeSet(_Player, _SetID);
//END_REGION

//REGION EFFECTS
//Unequipping will have a visual change
QRY
LLEQSET_QRY_SetManager_ShouldPlayEffect((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
AND
LLEQSET_QRY_SetManager_WillUnequip(_Player, _SetID)
THEN
DB_NOOP(1);

//Equipping will have a visual change
QRY
LLEQSET_QRY_SetManager_ShouldPlayEffect((CHARACTERGUID)_Player, (STRING)_SetID)
AND
NOT DB_LLEQSET_SetManager_CurrentSet(_Player, _)
AND
LLEQSET_QRY_SetExists(_Player, _SetID)
AND
LLEQSET_QRY_SetManager_SetEquipmentMismatch(_Player, _SetID)
THEN
DB_NOOP(1);

//Switching to a different set will have a visual change
QRY
LLEQSET_QRY_SetManager_ShouldPlayEffect((CHARACTERGUID)_Player, (STRING)_NextSetID)
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _CurrentSetID)
AND
_NextSetID != _CurrentSetID
AND
LLEQSET_QRY_SetManager_SetEquipmentMismatch(_Player, _NextSetID)
THEN
DB_NOOP(1);

PROC
LLEQSET_SetManager_PlayOverlayEffect((CHARACTERGUID)_Player, (STRING)_SetID)
THEN
PlayEffect(_Player, "LLEQSET_FX_SwitchSet_Overlay_01", "Dummy_Root");

PROC
LLEQSET_SetManager_PlayCastEffect((CHARACTERGUID)_Player, (STRING)_SetID)
THEN
PlayEffect(_Player, "LLEQSET_FX_SwitchSet_Cast_01", "Dummy_Root");
//END_REGION

//REGION AUTO_SAVING
IF
ItemAddedToContainer(_Item, _Backpack)
AND
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, _Item)
AND
DB_LLEQSET_SetManager_SetConfig(_SetID, _Skill, _ItemTemplate, _BackpackTemplate)
AND
GetTemplate(_Backpack, _BackpackTemplate)
THEN
DB_LLEQSET_SetManager_BackpackContents(_Player, _Backpack, _Item);

IF
ItemRemovedFromContainer(_Item, _Backpack)
AND
DB_LLEQSET_SetManager_BackpackContents(_Player, _Backpack, _Item)
THEN
NOT DB_LLEQSET_SetManager_BackpackContents(_Player, _Backpack, _Item);

IF
ObjectFlagSet("LLEQSET_Settings_AutoSaveEnabled", (CHARACTERGUID)_Player, _Instance)
THEN
LLEQSET_ColorStatusText(_Player, "[Equipment Sets] Auto-Save Enabled", "SetSaved");

IF
ObjectFlagCleared("LLEQSET_Settings_AutoSaveEnabled", (CHARACTERGUID)_Player, _Instance)
THEN
LLEQSET_ColorStatusText(_Player, "[Equipment Sets] Auto-Save Disabled", "Warning");

IF
ItemEquipped(_Item, _Player)
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
AND
ObjectGetFlag(_Player, "LLEQSET_Settings_AutoSaveEnabled", 1)
AND
NOT DB_LLEQSET_Temp_SetManager_DisableAutoSave(_Player)
AND
LLEQSET_QRY_GetSlot(_Player, _Item)
AND
DB_LLEQSET_Temp_ItemSlot(_Player, _Item, _Slot)
AND
GetStatString(_Item, _ItemID)
THEN
LLEQSET_SetManager_RegisterEquipment(_Player, _SetID, _Slot, _Item);
NOT DB_LLEQSET_Temp_ItemSlot(_Player, _Item, _Slot);
LeaderLog_Log("DEBUG","[LLEQSET:SetManager] Auto-saved item [",_ItemID,"] to Set [",_SetID,"] in slot [",_Slot,"]");

IF
ItemUnEquipped(_Item, _Player)
AND
DB_LLEQSET_SetManager_CurrentSet(_Player, _SetID)
AND
ObjectGetFlag(_Player, "LLEQSET_Settings_AutoSaveEnabled", 1)
AND
NOT DB_LLEQSET_Temp_SetManager_DisableAutoSave(_Player)
AND
DB_LLEQSET_SetManager_SavedSetEquipment(_Player, _SetID, _Slot, _Item)
THEN
LLEQSET_SetManager_ClearSavedInSlot(_Player, _SetID, _Slot);
LeaderLog_Log("DEBUG","[LLEQSET:SetManager] Auto-saved enabled, cleared item in Set [",_SetID,"] from slot [",_Slot,"]");
//END_REGION

//REGION CHANGE_SET_VAR_RESET
PROC
LLEQSET_SetManager_ChangeSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_Temp_SetManager_UnequippedSet(_Player, _SetID)
THEN
NOT DB_LLEQSET_Temp_SetManager_UnequippedSet(_Player, _SetID);

PROC
LLEQSET_SetManager_ChangeSet((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_Temp_SetManager_DisableAutoSave(_Player)
AND
GetUUID(_Player, _ID)
AND
LeaderLog_QRY_Log("COMBINE", "LLEQSET_Timers_", _ID, "_", _SetID, "_AutoSaveReset")
AND
DB_LeaderLog_Temp_CombinedString(_TimerName)
THEN
NOT DB_LeaderLog_Temp_CombinedString(_TimerName);
DB_LLEQSET_Temp_SetManager_ResetAutoSaveTimer(_Player, _TimerName);
TimerLaunch(_TimerName, 500);

IF
TimerFinished(_TimerName)
AND
DB_LLEQSET_Temp_SetManager_ResetAutoSaveTimer(_Player, _TimerName)
AND
NOT LLEQSET_QRY_PlayerEquipTimerActive(_Player)
THEN
LeaderLog_Log("DEBUG","[LLEQSET:SetManager] Auto-saved re-enabled.");
NOT DB_LLEQSET_Temp_SetManager_ResetAutoSaveTimer(_Player, _TimerName);
NOT DB_LLEQSET_Temp_SetManager_DisableAutoSave(_Player);

IF
TimerFinished(_TimerName)
AND
DB_LLEQSET_Temp_SetManager_ResetAutoSaveTimer(_Player, _TimerName)
AND
LLEQSET_QRY_PlayerEquipTimerActive(_Player)
THEN
TimerLaunch(_TimerName, 500);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_EquipmentSets"
