Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_LLEQSET_EquipmentSlots(_Slot)
//DB_LLEQSET_SystemColors(_Color, _ColorValue)
//DB_LLEQSET_DefaultColors(_Index, _ColorName, _ColorValue)
//DB_LLEQSET_PlayerColors(_Player, _ColorID, _ColorValue)
//DB_LLEQSET_MenuSkill(_Skill)
//DB_LLEQSET_MenuItem(_Template)
//DB_LLEQSET_Timers_DelayedMessage(_TimerName, _Player, _Message, _Color)
//DB_LLEQSET_Temp_NextMessageColor(_Player, _ColorID, _ColorValue)
//DB_LLEQSET_Temp_OutVariable(_ID, _Value)
//DB_LLEQSET_Temp_TracedCharacterValue(_Character, _Value)
//DB_LLEQSET_Temp_ItemSlot(_Player, _Item, _Slot)

DB_LLEQSET_PlayerColors((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000, "Default", "White");
LLEQSET_System_InitializeSettings();
KBSECTION
//REGION SETTINGS_COLORS
PROC
LLEQSET_System_InitColors()
THEN
LLEQSET_System_ClearExistingDatabase("DB_LLEQSET_SystemColors", 2);
DB_LLEQSET_SystemColors("FriendlyText", "00FA9A");
DB_LLEQSET_SystemColors("SetSaved", "00BFFF");
DB_LLEQSET_SystemColors("SetCleared", "FF4500");
DB_LLEQSET_SystemColors("SetChanged", "00FF7F");
DB_LLEQSET_SystemColors("UnequippedAll", "FF6347");
DB_LLEQSET_SystemColors("Error", "FF0000");
DB_LLEQSET_SystemColors("Warning", "FF8C00");
DB_LLEQSET_SystemColors("Set1", "FF7F50"); // Coral
DB_LLEQSET_SystemColors("Set2", "00BFFF"); // DeepSkyBlue
DB_LLEQSET_SystemColors("Set3", "FF69B4"); // HotPink ;)
DB_LLEQSET_SystemColors("Set4", "6A5ACD"); // SlateBlue
DB_LLEQSET_SystemColors("Set5", "FFD700"); // Gold

LLEQSET_System_ClearExistingDatabase("DB_LLEQSET_DefaultColors", 3);
DB_LLEQSET_DefaultColors(0, "Red", "FF0000");
DB_LLEQSET_DefaultColors(1, "Pink", "FFC0CB");
DB_LLEQSET_DefaultColors(2, "Orange", "FFA500");
DB_LLEQSET_DefaultColors(3, "Yellow", "FFFF00");
DB_LLEQSET_DefaultColors(4, "Gold", "FFD700");
DB_LLEQSET_DefaultColors(5, "Magenta", "FF00FF");
DB_LLEQSET_DefaultColors(6, "Purple", "800080");
DB_LLEQSET_DefaultColors(7, "Lime", "00FF00");
DB_LLEQSET_DefaultColors(8, "Green", "008000");
DB_LLEQSET_DefaultColors(9, "Cyan", "00FFFF");
DB_LLEQSET_DefaultColors(10, "Blue", "0000FF");
DB_LLEQSET_DefaultColors(11, "SkyBlue", "87CEEB");
DB_LLEQSET_DefaultColors(12, "Wheat", "F5DEB3");
DB_LLEQSET_DefaultColors(13, "Brown", "A52A2A");
DB_LLEQSET_DefaultColors(14, "Maroon", "800000");
DB_LLEQSET_DefaultColors(15, "White", "FFFFFF");
DB_LLEQSET_DefaultColors(16, "Azure", "F0FFFF");
DB_LLEQSET_DefaultColors(17, "Gray", "808080");
DB_LLEQSET_DefaultColors(18, "Black", "000000");
//END_REGION

//REGION SETTINGS

PROC
LLEQSET_System_InitializeSettings()
THEN
LLEQSET_System_ClearExistingDatabase("DB_LLEQSET_EquipmentSlots", 1);
DB_LLEQSET_EquipmentSlots("Weapon");
DB_LLEQSET_EquipmentSlots("Shield");
DB_LLEQSET_EquipmentSlots("Helmet");
DB_LLEQSET_EquipmentSlots("Breast");
DB_LLEQSET_EquipmentSlots("Leggings");
DB_LLEQSET_EquipmentSlots("Ring");
DB_LLEQSET_EquipmentSlots("Belt");
DB_LLEQSET_EquipmentSlots("Boots");
DB_LLEQSET_EquipmentSlots("Gloves");
DB_LLEQSET_EquipmentSlots("Amulet");
DB_LLEQSET_EquipmentSlots("Ring2");

LLEQSET_System_InitColors();

LLEQSET_System_ClearExistingDatabase("DB_LLEQSET_MenuSkill", 1);
DB_LLEQSET_MenuSkill("Shout_LLEQSET_OpenSetMenu");

LLEQSET_System_ClearExistingDatabase("DB_LLEQSET_MenuItem", 1);
DB_LLEQSET_MenuItem("LLEQSET_BOOK_MenuBook_42227c69-e194-4de6-9771-c05f39aa3f46");

SysClear("DB_LLEQSET_Temp_NextMessageColor", 3);
SysClear("DB_LLEQSET_Temp_OutVariable", 2);
SysClear("DB_LLEQSET_Temp_TracedCharacterValue", 2);
SysClear("DB_LLEQSET_Temp_ItemSlot", 3);

PROC
LLEQSET_System_UpdateDatabases()
THEN
LLEQSET_System_InitializeSettings();

PROC
LLEQSET_System_ClearDatabases()
THEN
SysClear("DB_LLEQSET_EquipmentSlots", 1);
SysClear("DB_LLEQSET_SystemColors", 2);
SysClear("DB_LLEQSET_DefaultColors", 3);
SysClear("DB_LLEQSET_PlayerColors", 3);
SysClear("DB_LLEQSET_MenuSkill", 1);
SysClear("DB_LLEQSET_MenuItem", 1);
SysClear("DB_LLEQSET_System_PlayerInitialized", 1);
SysClear("DB_LLEQSET_Temp_NextMessageColor", 3);
SysClear("DB_LLEQSET_Temp_OutVariable", 2);
SysClear("DB_LLEQSET_Temp_TracedCharacterValue", 2);
SysClear("DB_LLEQSET_Temp_ItemSlot", 3);

DebugBreak("[LLEQSET] System databases cleared.");

//END_REGION

//REGION DATABASE_HELPERS
PROC
LLEQSET_System_ClearExistingDatabase((STRING)_DatabaseName, (INTEGER)_Arity)
AND
SysCount(_DatabaseName, _Arity, _Count)
AND
_Count > 0
THEN
SysClear(_DatabaseName, _Arity);

PROC
LLEQSET_System_Debug_DatabaseCount((STRING)_DatabaseName, (INTEGER)_Arity)
AND
SysCount(_DatabaseName, _Arity, _Total)
AND
IntegertoString(_Total, _TotalStr)
AND
StringConcatenate("[LLEQSET] '", _DatabaseName, _Str1)
AND
StringConcatenate(_Str1, "' Total Entries: ", _Str2)
AND
StringConcatenate(_Str2, _TotalStr, _Str3)
THEN
DebugBreak(_Str3);
//END_REGION

//REGION CHARACTER_TRACING
QRY
LLEQSET_System_GetTracedCharacterValue((CHARACTERGUID)_Character, (STRING)_ValueName)
AND
DB_LLEQSET_Temp_TracedCharacterValue(_Character, _Value)
THEN
NOT DB_LLEQSET_Temp_TracedCharacterValue(_Character, _Value);

QRY
LLEQSET_System_GetTracedCharacterValue((CHARACTERGUID)_Character, "nameref")
AND
CharacterGetDisplayName(_Character, _Handle, _Ref)
THEN
DB_LLEQSET_Temp_TracedCharacterValue(_Character, _Ref);

QRY
LLEQSET_System_GetTracedCharacterValue((CHARACTERGUID)_Character, "namehandle")
AND
CharacterGetDisplayName(_Character, _Handle, _Ref)
THEN
DB_LLEQSET_Temp_TracedCharacterValue(_Character, _Handle);

QRY
LLEQSET_System_GetTracedCharacterValue((CHARACTERGUID)_Character, "id")
AND
GetUUID(_Character, _ID)
THEN
DB_LLEQSET_Temp_TracedCharacterValue(_Character, _ID);

QRY
LLEQSET_System_GetTracedCharacterValue((CHARACTERGUID)_Character, "template")
AND
GetTemplate(_Character, _Template)
THEN
DB_LLEQSET_Temp_TracedCharacterValue(_Character, _Template);
//END_REGION

//REGION TEMP_VARIABLES
PROC
LLEQSET_SetTemporaryVariable((STRING)_ID, (STRING)_Value)
AND
DB_LLEQSET_Temp_OutVariable(_ID, _CurrentValue)
THEN
NOT DB_LLEQSET_Temp_OutVariable(_ID, _CurrentValue);

PROC
LLEQSET_SetTemporaryVariable((STRING)_ID, (STRING)_Value)
THEN
DB_LLEQSET_Temp_OutVariable(_ID, _Value);

QRY
LLEQSET_QRY_SetTemporaryVariable((STRING)_ID, (STRING)_Value)
THEN
LLEQSET_SetTemporaryVariable(_ID, _Value);
//END_REGION

//REGION COLOR_FETCHING_SYSTEM
QRY
LLEQSET_QRY_ColorExists((STRING)_ColorName)
AND
DB_LLEQSET_SystemColors(_ColorName, _)
THEN
DB_NOOP(1);

QRY
LLEQSET_QRY_ColorExists((STRING)_ColorName)
AND
DB_LLEQSET_DefaultColors(_, _ColorName, _)
THEN
DB_NOOP(1);

QRY
LLEQSET_QRY_ColorExists((CHARACTERGUID)_Player, (STRING)_ColorName)
AND
DB_LLEQSET_PlayerColors(_,_,_ColorName)
THEN
DB_NOOP(1);

QRY
LLEQSET_QRY_FetchColor((STRING)_ID, (STRING)_Color)
AND
DB_LLEQSET_SystemColors(_Color, _ColorValue)
THEN
LLEQSET_SetTemporaryVariable(_ID, _ColorValue);

QRY
LLEQSET_QRY_FetchColor((STRING)_ID, (STRING)_Color)
AND
DB_LLEQSET_DefaultColors(_Index, _Color, _ColorValue)
THEN
LLEQSET_SetTemporaryVariable(_ID, _ColorValue);

//END_REGION

//REGION COLOR_FETCHING_PLAYER
QRY
LLEQSET_QRY_FetchPlayerColor((CHARACTERGUID)_Player, (STRING)_ColorID)
AND
DB_LLEQSET_Temp_NextMessageColor(_Player, _ColorID, _Value)
THEN
NOT DB_LLEQSET_Temp_NextMessageColor(_Player, _ColorID, _Value);

// Check for a color name first
QRY
LLEQSET_QRY_FetchPlayerColor((CHARACTERGUID)_Player, (STRING)_ColorID)
AND
DB_LLEQSET_PlayerColors(_Player, _ColorID, _ColorValue)
AND
GetUUID(_Player, _ID)
AND
LLEQSET_QRY_FetchColor(_ID, _ColorValue)
AND
DB_LLEQSET_Temp_OutVariable(_ID, _Value)
THEN
DB_LLEQSET_Temp_NextMessageColor(_Player, _ColorID, _Value);
NOT DB_LLEQSET_Temp_OutVariable(_ID, _Value);

//If the stored value isn't a color name, use it explicitly instead
QRY
LLEQSET_QRY_FetchPlayerColor((CHARACTERGUID)_Player, (STRING)_ColorID)
AND
DB_LLEQSET_PlayerColors(_Player, _ColorID, _ColorValue)
AND
GetUUID(_Player, _ID)
AND
NOT LLEQSET_QRY_FetchColor(_ID, _ColorValue)
THEN
DB_LLEQSET_Temp_NextMessageColor(_Player, _ColorID, _ColorValue);

//Use a color value if no player color exists
QRY
LLEQSET_QRY_FetchPlayerColor((CHARACTERGUID)_Player, (STRING)_ColorID)
AND
NOT DB_LLEQSET_PlayerColors(_Player, _ColorID,_)
AND
GetUUID(_Player, _ID)
AND
LLEQSET_QRY_FetchColor(_ID, _ColorID)
AND
DB_LLEQSET_Temp_OutVariable(_ID, _Value)
THEN
DB_LLEQSET_Temp_NextMessageColor(_Player, _ColorID, _Value);
NOT DB_LLEQSET_Temp_OutVariable(_ID, _Value);

//Use the value supplied if it's not a color name
QRY
LLEQSET_QRY_FetchPlayerColor((CHARACTERGUID)_Player, (STRING)_ColorID)
AND
NOT DB_LLEQSET_PlayerColors(_Player, _ColorID,_)
AND
GetUUID(_Player, _ID)
AND
NOT LLEQSET_QRY_FetchColor(_ID, _ColorID)
THEN
DB_LLEQSET_Temp_NextMessageColor(_Player, _ColorID, _ColorID);

QRY
LLEQSET_QRY_System_RegisterColor((CHARACTERGUID)_Player, (STRING)_ColorID, (STRING)_ColorValue)
AND
NOT DB_LLEQSET_PlayerColors(_Player, _ColorID,_)
AND
LLEQSET_System_GetTracedCharacterValue(_Player, "nameref")
AND
DB_LLEQSET_Temp_TracedCharacterValue(_Player, _Name)
THEN
DB_LLEQSET_PlayerColors(_Player, _ColorID, _ColorValue);
LeaderLog_Log("DEBUG","[LLEQSET:System] Player [",_Name,"] color [", _ColorID, "] registered.");
NOT DB_LLEQSET_Temp_TracedCharacterValue(_Player, _Name);

//END_REGION

//REGION MESSAGE_METHODS
QRY
LLEQSET_QRY_ColorStatusText((CHARACTERGUID)_Player, (STRING)_Message, (STRING)_Color)
THEN
LLEQSET_ColorStatusText(_Player, _Message, _Color);

PROC
LLEQSET_ColorStatusText((CHARACTERGUID)_Player, (STRING)_Message, (STRING)_Color)
AND
LLEQSET_QRY_FetchPlayerColor(_Player, _Color)
AND
DB_LLEQSET_Temp_NextMessageColor(_Player, _Color, _ColorValue)
THEN
LLEQSET_Internal_ColorStatusText(_Player, _Message, _ColorValue);
NOT DB_LLEQSET_Temp_NextMessageColor(_Player, _Color, _ColorValue);

PROC
LLEQSET_Internal_ColorStatusText((CHARACTERGUID)_Player, (STRING)_Message, (STRING)_Color)
AND
StringConcatenate("<font color='#", _Color, _Str1)
AND
StringConcatenate(_Str1, "'>", _Str2)
AND
StringConcatenate(_Str2, _Message, _Str3)
AND
StringConcatenate(_Str3, "</font>", _Str4)
THEN
CharacterStatusText(_Player, _Str4);

QRY
LLEQSET_QRY_ColorSetText((CHARACTERGUID)_Player, (STRING)_SetID, (STRING)_Color, (STRING)_Prefix, (STRING)_Suffix)
THEN
LLEQSET_ColorSetText(_Player, _SetID, _Color, _Prefix, _Suffix);

PROC
LLEQSET_ColorSetText((CHARACTERGUID)_Player, (STRING)_SetID, (STRING)_Color, (STRING)_Prefix, (STRING)_Suffix)
AND
LLEQSET_QRY_Helpers_GetSetName(_Player, _SetID)
AND
DB_LLEQSET_Temp_SetName(_Player, _SetID, _SetName)
AND
LLEQSET_QRY_FetchPlayerColor(_Player, _SetID)
AND
DB_LLEQSET_Temp_NextMessageColor(_Player, _SetID, _ColorValue)
AND
//SET_COLORING_START
StringConcatenate("<font color='#", _ColorValue, _SetStr1)
AND
StringConcatenate(_SetStr1, "' size='24'>", _SetStr2)
AND
StringConcatenate(_SetStr2, _SetName, _SetStr3)
AND
StringConcatenate(_SetStr3, "</font>", _SetStrFinal)
//SET_COLORING_END
AND
//PREFIX_COLORING
LLEQSET_QRY_FetchPlayerColor(_Player, _Color)
AND
DB_LLEQSET_Temp_NextMessageColor(_Player, _Color, _MessageColor)
AND
StringConcatenate("<font color='#", _MessageColor, _PrefixStr1)
AND
StringConcatenate(_PrefixStr1, "'>", _PrefixStr2)
AND
StringConcatenate(_PrefixStr2, _Prefix, _PrefixStr3)
AND
StringConcatenate(_PrefixStr3, "</font>", _PrefixStrFinal)
//PREFIX_END
AND
//SUFFIX_START
StringConcatenate("<font color='#", _MessageColor, _SuffixStr1)
AND
StringConcatenate(_SuffixStr1, "'>", _SuffixStr2)
AND
StringConcatenate(_SuffixStr2, _Suffix, _SuffixStr3)
AND
StringConcatenate(_SuffixStr3, "</font>", _SuffixStrFinal)
//SUFFIX_END
AND
//FINAL_COMBINING
StringConcatenate(_PrefixStrFinal, _SetStrFinal, _FinalStr1)
AND
StringConcatenate(_FinalStr1, _SuffixStrFinal, _FinalStr2)
THEN
CharacterStatusText(_Player, _FinalStr2);
NOT DB_LLEQSET_Temp_SetName(_Player, _SetID, _SetName);
NOT DB_LLEQSET_Temp_NextMessageColor(_Player, _SetID, _ColorValue);
NOT DB_LLEQSET_Temp_NextMessageColor(_Player, _Color, _MessageColor);
//END_REGION

//REGION FLAG_HELPERS
QRY
LLEQSET_QRY_ClearObjectFlag((GUIDSTRING)_Object, (STRING)_Flag)
THEN
ObjectClearFlag(_Object, _Flag);
//END_REGION

//REGION SET_HELPERS
QRY
LLEQSET_QRY_Helpers_GetSetName((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_Temp_SetName(_Player, _SetID, _SetName)
THEN
NOT DB_LLEQSET_Temp_SetName(_Player, _SetID, _SetName);

QRY
LLEQSET_QRY_Helpers_GetSetName((CHARACTERGUID)_Player, (STRING)_SetID)
AND
NOT DB_LLEQSET_SetManager_SetNames(_Player, _SetID,_)
AND
StringSub(_SetID, 3, 1, _SetNumber)
AND
StringConcatenate("Set ", _SetNumber, _SetName)
THEN
DB_LLEQSET_Temp_SetName(_Player, _SetID, _SetName);

QRY
LLEQSET_QRY_Helpers_GetSetName((CHARACTERGUID)_Player, (STRING)_SetID)
AND
DB_LLEQSET_SetManager_SetNames(_Player, _SetID, _Name)
THEN
DB_LLEQSET_Temp_SetName(_Player, _SetID, _Name);

QRY
LLEQSET_QRY_GetSlot((CHARACTERGUID)_Player, (ITEMGUID)_Item)
AND
DB_LLEQSET_Temp_ItemSlot(_Player, _Item, _Slot)
THEN
NOT DB_LLEQSET_Temp_ItemSlot(_Player, _Item, _Slot);

QRY
LLEQSET_QRY_GetSlot((CHARACTERGUID)_Player, (ITEMGUID)_Item)
AND
DB_LLEQSET_EquipmentSlots(_Slot)
AND
CharacterGetEquippedItem(_Player, _Slot, _Item)
THEN
DB_LLEQSET_Temp_ItemSlot(_Player, _Item, _Slot);
//END_REGION

//REGION TIMER_HELPERS
IF
TimerFinished(_TimerName)
AND
DB_LLEQSET_Timers_DelayedMessage(_TimerName, _Player, _Message, _Color)
THEN
LLEQSET_ColorStatusText(_Player, _Message, _Color);
NOT DB_LLEQSET_Timers_DelayedMessage(_TimerName, _Player, _Message, _Color);
//END_REGION

//REGION HOST_FLAGGING
PROC
LLEQSET_System_MarkHost((CHARACTERGUID)_Player)
AND
CharacterGetHostCharacter(_Player)
THEN
UserSetFlag(_Player, "LLEQSET_ISHOST");

PROC
LLEQSET_System_MarkHost((CHARACTERGUID)_Player)
AND
NOT CharacterGetHostCharacter(_Player)
AND
UserGetFlag(_Player, "LLEQSET_ISHOST", 1)
THEN
UserClearFlag(_Player, "LLEQSET_ISHOST", 0);

PROC
LLEQSET_System_ClearInitialization((CHARACTERGUID)_Player)
AND
DB_LLEQSET_System_PlayerInitialized(_Player)
THEN
NOT DB_LLEQSET_System_PlayerInitialized(_Player);

PROC
LLEQSET_System_InitialSetup((CHARACTERGUID)_Player)
AND
NOT DB_LLEQSET_System_PlayerInitialized(_Player)
AND
DB_LLEQSET_MenuSkill(_Skill)
AND
CharacterHasSkill(_Player, _Skill, 0)
THEN
CharacterAddSkill(_Player, _Skill);

PROC
LLEQSET_System_InitialSetup((CHARACTERGUID)_Player)
AND
NOT DB_LLEQSET_System_PlayerInitialized(_Player)
THEN
DB_LLEQSET_System_PlayerInitialized(_Player);

IF
GameStarted(_,_)
AND
DB_IsPlayer(_Player)
THEN
LLEQSET_System_MarkHost(_Player);
LLEQSET_System_InitialSetup(_Player);
//END_REGION

//REGION DEBUG
IF
CharacterUsedItem(_Player, ITEMGUID_Lever_OpenNamingMenu_4289a1de-0d4b-43b0-9c38-0d796dff1d43)
AND
DB_LLEQSET_SetManager_SetConfig(_SetID, _Skill, _ItemTemplate, _BackpackTemplate)
AND
GetItemForItemTemplateInInventory(_Player, _BackpackTemplate, _Backpack)
AND
GetPosition(_Player, _x,_y,_z)
THEN
TeleportToPosition(_Backpack,  _x,_y,_z, "", 0);
//SetStoryEvent(_Player, "LLNWIZ_Events_OpenNamingWizard_PlayerOnly");

IF
CharacterUsedItem(_Player, ITEMGUID_Lever_OpenMainMenu_bac439cf-9197-49f1-8b5a-91ca40d050ec)
THEN
LLEQSET_Menu_OpenMenu(_Player);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_EquipmentSets"
